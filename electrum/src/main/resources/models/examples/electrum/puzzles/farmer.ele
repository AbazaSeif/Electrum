module examples/puzzles/farmer

/*
 * The classic river crossing puzzle. A farmer is carrying a fox, a
 * chicken, and a sack of grain. He must cross a river using a boat
 * that can only hold the farmer and at most one other thing. If the
 * farmer leaves the fox alone with the chicken, the fox will eat the
 * chicken; and if he leaves the chicken alone with the grain, the
 * chicken will eat the grain. How can the farmer bring everything
 * to the far side of the river intact?
 *
 * authors: Greg Dennis, Rob Seater
 *
 * Acknowledgements to Derek Rayside and his students for finding and
 * fixing a bug in the "crossRiver" predicate.
 */

/**
 * The farmer and all his possessions will be represented as Objects.
 * Some objects eat other objects when the Farmer's not around.
 */
abstract sig Object { eats: set Object }
one sig Farmer, Fox, Chicken, Grain extends Object {}

/**
 * Define what eats what when the Farmer' not around.
 * Fox eats the chicken and the chicken eats the grain.
 */
fact eating { eats = Fox->Chicken + Chicken->Grain }

/**
 * The near and far relations contain the objects held on each
 * side of the river in a given state, respectively.
 */
var sig near, far in Object {}

/**
 * In the initial state, all objects are on the near side.
 */
fact initialState {
  near = Object && no far
}

/**
 * Constrains at most one item to move from 'from' to 'to'.
 * Also constrains which objects get eaten.
 */
pred crossRiver [from, from2, to, to2: set Object] {
   // either the Farmer takes no items
   (from2 = from - Farmer - from2.eats and
    to2 = to + Farmer) or
    // or the Farmer takes one item
    (one x : from - Farmer | {
       from2 = from - Farmer - x - from2.eats
       to2 = to + Farmer + x })
}

/**
 * crossRiver transitions between states
 */
fact stateTransition {
  always {
    Farmer in near =>
      crossRiver[near, near', far, far'] else
      crossRiver[far, far', near, near']
  }
}

/**
 * the farmer moves everything to the far side of the river.
 */
pred solvePuzzle {
     eventually far = Object
}

run solvePuzzle for 8 Time expect 1

/**
 * no Object can be in two places at once
 * this is implied by both definitions of crossRiver
 */
assert NoQuantumObjects {
   always no x : Object | x in near and x in far
}

check NoQuantumObjects for 8 Time expect 0
