module examples/algorithms/stable_mutex_ring

/*
 * Dijkstra's K-state mutual exclusion algorithm for a ring
 *
 * Original paper describing the algorithm:
 *   [1] E.W.Dijkstra, "Self-Stabilizing Systems in Spite of
 *   Distributed Control", Comm. ACM, vol. 17, no. 11, pp.
 *   643-644, Nov. 1974
 *
 * Proof of algorithm's correctness:
 *   [2] E.W.Dijkstra, "A Belated Proof of Self-Stabilization",
 *   in Distributed Computing, vol. 1, no. 1, pp. 5-6, 1986
 *
 * SMV analysis of this algorithm is described in:
 *   [3] "Symbolic Model Checking for Self-Stabilizing Algorithms",
 *   by Tatsuhiro Tsuchiya, Shini'ichi Nagano, Rohayu Bt Paidi, and
 *   Tohru Kikuno, in IEEE Transactions on Parallel and Distributed
 *   Systems, vol. 12, no. 1, January 2001
 *
 * Description of algorithm (adapted from [3]):
 *
 * Consider a distributed system that consists of n processes
 * connected in the form of a ring.  We assume the state-reading
 * model in which processes can directly read the state of their
 * neighbors.  We define _privilege_ of a process as its ability to
 * change its current state.  This ability is based on a Boolean
 * predicate that consists of its current state and the state of
 * one of its neighboring processes.
 *
 * We then define the legitimate states as those in which the
 * following two properties hold: 1) exactly one process has a
 * privilege, and 2) every process will eventually have a privilege.
 * These properties correspond to a form of mutual exclusion, because
 * the privileged process can be regarded as the only process that is
 * allowed in its critical section.
 *
 * In the K-state algorithm, the state of each process is in
 * {0,1,2,...,K-1}, where K is an integer larger than or equal to n.
 * For any process p_i, we use the symbols S and L to denote its
 * state and the state of its neighbor p_{i-1}, respectively, and
 * process p_0 is treated differently from all other processes. The
 * K-state algorithm is described below.
 *
 *   process p_0: if (L=S) { S := (S+1) mod K; }
 *   process P_i(i=1,...,n-1): if (L!=S) { S:=L; }
 */

open util/graph[Process] as pg
open util/graph[Val] as vg

sig Process {
  rightNeighbor: Process,
  var val: one Val,
}
var sig runs in Process {} // processes scheduled to run on this tick
  // for visualization
var sig priv in Process {} // the set of priviledged processes on this tick
fact {
  always priv = { p : Process | Privileged[p] }
}

sig Val {
  nextVal : Val
}

fact MoreValThanProcess {
  gt[# Val , # Process]
}

fact DefineRings {
  pg/ring[rightNeighbor]
  vg/ring[nextVal]
  //Val$nextVal = Ord[Val].next + (Ord[Val].last -> Ord[Val].first)
}

one sig FirstProc extends Process {
}


fun FirstProcTrans[curVal, neighborVal : Val]: Val {
  (curVal = neighborVal) => curVal.nextVal else curVal
}

fun RestProcTrans[curVal, neighborVal : Val]: Val {
  (curVal != neighborVal) => neighborVal else curVal
}

fact LegalTrans {
  always {
        all p: Process |
           let curVal = val[p], neighborVal = val[p.rightNeighbor], newVal = val'[p] | {
                p !in runs => newVal = curVal else {
                   p = FirstProc =>
                       newVal = FirstProcTrans[curVal, neighborVal]
                   else
                       newVal = RestProcTrans[curVal, neighborVal]
                }
          }
      }
}

pred TickTrans {
  all p : Process |
    let curVal = val[p], neighborVal = val[p.rightNeighbor], newVal = val'[p] | {
                   p = FirstProc =>
                       newVal = FirstProcTrans[curVal, neighborVal]
                   else
                       newVal = RestProcTrans[curVal, neighborVal]
    }
}

/**
 * whether this process can enter its critical section
 * on this tick
 */  
pred Privileged[p : Process] {
  p = FirstProc =>
    val[p] = val[p.rightNeighbor]
  else
    val[p] != val[p.rightNeighbor]
}

pred IsomorphicStates[val1, val2: Process -> one Val] {
   some processMap: Process one -> one Process,
        valMap: Val one -> one Val | {
       FirstProc.processMap = FirstProc
       all p: Process, v: Val |  {
          p->v in val1 iff (p.processMap) -> (v.valMap) in val2
       }
       all v1,v2: Val | v1->v2 in nextVal iff (v1.valMap) ->  (v2.valMap) in nextVal
       all p1,p2: Process | p1->p2 in rightNeighbor
               iff (p1.processMap) ->  (p2.processMap) in rightNeighbor
   }
}

/**
 * Find a trace that goes into a loop
 * containing a bad tick, i.e. a tick
 * at which two distinct processes
 * try to run their critical sections
 * simultaneously.  In such a trace the
 * algorithm never "stabilizes".
 */
pred BadSafetyTrace {
      eventually Process in runs
      eventually BadTick
}

/**
 * Two different processes simultaneously
 * try to run their critical sections at this tick
 */
pred BadTick {
      some p1 , p2 : Process | {
        p1!=p2
        Privileged[p1]
        Privileged[p2]
      }
}

assert Closure {
  not BadTick => (always not BadTick)
}

pred TwoPrivileged {
  BadTick
  some disj p1, p2 : Process {
    eventually Privileged[p1]
    eventually Privileged[p2]
  }
}

/*pred TraceWithoutLoop  {
  all t1, t2 : Tick | t1!=t2 => t1.val != t2.val
}*/

pred TraceShorterThanMaxSimpleLoop {
// ?????
}

run TraceShorterThanMaxSimpleLoop for 7 but 2 Process, 3 Val expect 1
run TwoPrivileged for 5 but exactly 8 Time, 3 Process, 4 Val, 4 int expect 1
check Closure for 5 but exactly 5 Time, 5 Process, 6 Val, 4 int expect 0
//run BadSafetyTrace for 16 but 3 Process, 4 Val
//run TraceWithoutLoop for 21 but 4 Process, 5 Val


